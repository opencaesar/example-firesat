/* 
 * The Maven coordinates for the project artifact
 */
description='The FireSat example project'
group = 'io.opencaesar.ontologies'
version = '1.2.0'

/* 
 * The Gradle task dependencies 
 */
buildscript {
	repositories {
		mavenLocal()
		maven { url 'https://dl.bintray.com/opencaesar/owl-adapter' }
		maven { url 'https://dl.bintray.com/opencaesar/owl-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml' }
		maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
		jcenter()
	}
	dependencies {
		classpath 'io.opencaesar.owl:owl-query-gradle:+'
		classpath 'io.opencaesar.owl:owl-load-gradle:+'
		classpath 'io.opencaesar.owl:owl-reason-gradle:+'
		classpath 'io.opencaesar.owl:oml2owl-gradle:+'
		classpath 'io.opencaesar.oml:oml-bikeshed-gradle:+'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
		classpath 'com.wiredforcode:gradle-spawn-plugin:+'
		classpath 'io.opencaesar.docbook:docbook-adapter-gradle:+'
		// needed since gradle bintray brings an old version of xerces
		configurations.classpath.exclude group: 'xerces', module: 'xercesImpl'
	}
}

/*
 * Dependency versions
 */
ext { 
	vocabulariesVersion = '1.0.+' 
	fusekiVersion = '3.16.0' 
}

/*
 * The configuration of OML dependencies
 */
configurations {
	oml // Include the oml dependencies only
    fuseki //Include fuseki server
    stylesheets // Include the docbook stylesheets
}

/*
 * The repositories to look up OML dependencies in
 */
repositories {
	mavenLocal()
	maven { url 'https://dl.bintray.com/opencaesar/vocabularies' }
	jcenter()
}

/*
 * The OML dependencies
 */
dependencies {
	oml "io.opencaesar.ontologies:vocabularies:$vocabulariesVersion"
    fuseki "org.apache.jena:jena-fuseki-server:$fusekiVersion"
    stylesheets "io.opencaesar.docbook:docbook-stylesheets:+"
}

/*
 * A task to extract and merge the OML dependencies
 */
task omldependencies(type: Copy) {
    from configurations.oml.files.collect { zipTree(it) }
    into file('build/dependencies')
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task oml2owl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: omldependencies) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL catalog
	outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlreason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: oml2owl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://opencaesar.io/programs/earth-science/projects/firesat/bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/classes = ALL_SUBCLASS',
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/reasoning.xml')
}

task owlload(type:io.opencaesar.owl.load.OwlLoadTask, dependsOn: owlreason) {
	catalogPath = file('build/owl/catalog.xml')
	endpointURL = 'http://localhost:3030/firesat'
    fileExtensions = ['owl', 'ttl']
}

task owlquery(type:io.opencaesar.owl.query.OwlQueryTask) {
	endpointURL = 'http://localhost:3030/firesat'
	queryPath = file('src/sparql')
	resultPath = file('build/frames')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task oml2bikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask, dependsOn: omldependencies) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL folder
	outputFolderPath = file('build/bikeshed')
	// Input Ontology Iri
	rootOntologyIri = 'http://opencaesar.io/programs/earth-science/projects/firesat/bundle'
	// Publish URL
	publishUrl = 'https://opencaesar.github.io/vocabularies/'
}

/*
 * A task to render the Bikeshed specification to HTML
 */
task bikeshed2html(dependsOn: oml2bikeshed) {
	doLast {
		exec { commandLine 'chmod', '+x', 'build/bikeshed/publish.sh' }
		exec { commandLine 'build/bikeshed/publish.sh' }
	}
}

/*
 * A task to generate a publishable Zip archive for the OML sources of this project
 */
task omlzip(type: Zip) {
	from file('src/oml')
	destinationDir(file('build/libs'))
	archiveBaseName = project.name
	archiveVersion = project.version
}

/*
 * Zips stylesheets from docbook-stylesheets (local maven)
 * Places them in build/stylesheets-gen 
 */
task stylesheetUnzip(type: Copy) {
  from configurations.stylesheets.files.collect { zipTree(it) }
  into file("build/stylesheets-gen")
}
/*
 * Gradle tasks to render the docbook physical_structure
 */
task physicalTag(type:io.opencaesar.docbook.adapter.DocbookAdapterTask, dependsOn: stylesheetUnzip) {
	input = file('src/docbook/physical/src/physical_structure.xml')
	type = 'tag'
	xsl = file('build/stylesheets-gen/tag/all_transformations.xsl')
	original = file('build/stylesheets-gen/docbook_xsl')
	frame = file('build/frames')	
}

task physicalPDF(type:io.opencaesar.docbook.adapter.DocbookAdapterTask) {
	input = file('src/docbook/physical/src-gen/physical_structure.xml')
	type = 'pdf'
	xsl = file('src/docbook/physical/src-gen/pdf/pdf_ext.xsl')
}

task physicalHTML(type:io.opencaesar.docbook.adapter.DocbookAdapterTask) {
	input = file('src/docbook/physical/src-gen/physical_structure.xml')
	type = 'html'
	xsl = file('src/docbook/physical/src-gen/html/html_ext.xsl')
	css = file('../docbook-tools/docbook-tools/docbook-stylesheets/src/stylesheets/default.css')
}

physicalTag.mustRunAfter(owlquery)
physicalHTML.mustRunAfter(physicalTag)
physicalPDF.mustRunAfter(physicalTag)

task physicalRender() {
	dependsOn owlquery
	dependsOn physicalTag
	dependsOn physicalPDF
	dependsOn physicalHTML
}

/*
 * Gradle tasks to render the docbook wbs document
 */
task wbsTag(type:io.opencaesar.docbook.adapter.DocbookAdapterTask, dependsOn: stylesheetUnzip) {
	input = file('src/docbook/wbs/src/wbs.xml')
	type = 'tag'
	xsl = file('build/stylesheets-gen/tag/all_transformations.xsl')
	original = file('build/stylesheets-gen/docbook_xsl')
	frame = file('build/frames')	
}

task wbsPDF(type:io.opencaesar.docbook.adapter.DocbookAdapterTask) {
	input = file('src/docbook/wbs/src-gen/wbs.xml')
	type = 'pdf'
	xsl = file('src/docbook/wbs/src-gen/pdf/pdf_ext.xsl')
}

task wbsHTML(type:io.opencaesar.docbook.adapter.DocbookAdapterTask) {
	input = file('src/docbook/wbs/src-gen/wbs.xml')
	type = 'html'
	xsl = file('src/docbook/wbs/src-gen/html/html_ext.xsl')
	css = file('../docbook-tools/docbook-tools/docbook-stylesheets/src/stylesheets/default.css')
}

wbsTag.mustRunAfter(owlquery)
wbsPDF.mustRunAfter(wbsTag)
wbsHTML.mustRunAfter(wbsTag)

task wbsRender() {
	dependsOn owlquery
	dependsOn wbsTag
	dependsOn wbsPDF
	dependsOn wbsHTML
}


/*
 * A task to build the project, which executes several tasks together
 */
task build() {
	dependsOn omlzip
	dependsOn owlreason
	dependsOn bikeshed2html
}

/*
 * A task to delete the build artifacts
 */
task clean(type: Delete) {
	delete 'build'
}

/*
 * Publish to Maven spec
 */
apply plugin: 'maven-publish'

publishToMavenLocal.dependsOn omlzip

publishing {
    publications {
        maven(MavenPublication) {
            artifact omlzip
        }
    }
}

/*
 * Publish to Bintrary Spec
 */
apply plugin: 'com.jfrog.bintray'

ext.gh = [
	dep: 'https://github.com',
	org: 'opencaesar',
	rep: 'example-firesat'
]

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_API_KEY')
	publications = ['maven']
	publish = true
	override = true
	pkg {
		name = project.name
		repo = gh.rep
		userOrg = gh.org
		desc = project.description
		licenses = ['Apache-2.0']
		websiteUrl = gh.dep+'/'+gh.org+'/'+gh.rep
        issueTrackerUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'/issues'
		vcsUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'.git'
		version {
			name = project.version
			desc = project.version
			released  = new Date()
		}
	}

}

/*
 * Start and stop the Fuseki server
 */
apply plugin: 'com.wiredforcode.spawn'

task downloadFuseki(type: Copy) {
    from configurations.fuseki
    into file("build/libs")
}

task startFuseki(type: SpawnProcessTask, dependsOn: downloadFuseki) {
	command "java -jar ${projectDir}/build/libs/jena-fuseki-server-${fusekiVersion}.jar --mem firesat"
	ready "Apache Jena Fuseki ${fusekiVersion}"
}

task stopFuseki(type: KillProcessTask)

